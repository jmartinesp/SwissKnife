import com.android.build.gradle.tasks.MergeResources

gradle.taskGraph.whenReady {
    allprojects {
        tasks.withType(GroovyCompile) { task ->
            logger.lifecycle("Task $task")
            task.groovyOptions.forkOptions.jvmArgs = ['-noverify']
        }
        tasks.withType(MergeResources) { task ->
            task.process9Patch = false
        }
    }
}

tasks.whenTaskAdded {
    if (it.name.contains('lint') || it.name.contains('Lint')) {
        it.enabled = false
    }
}

apply plugin: 'com.android.application'
apply plugin: 'groovyx.grooid.groovy-android'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "com.dexafree.sample"
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }

    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
    }

    buildTypes {
        release {
            minifyEnabled false
            // proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':SwissKnife')
    compile 'org.codehaus.groovy:groovy:2.4.0:grooid'
}
